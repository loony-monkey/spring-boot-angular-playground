buildscript {
    repositories { mavenCentral() }
    dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE") }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    version =  '0.1.0-SNAPSHOT'
    group = "de.loonymonkey.playground"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories { mavenCentral() }

    dependencies {
        compile "commons-lang:commons-lang:2.6"
        compile "com.google.guava:guava:18.0"
        testCompile "junit:junit:4.12"
    }
}

project(":domain-model") {
    jar { baseName = 'domain-model' }

    dependencies {
        compile "org.springframework:spring-context:4.1.6.RELEASE"
    }
}

project(":persistence") {
    jar { baseName = 'persistence' }

    dependencies {
        compile project(":domain-model")
    }
}

project(":frontend") {
    jar { baseName = 'frontend' }

    apply plugin: 'spring-boot'

    dependencies {
        // tag::jetty[]
        compile("org.springframework.boot:spring-boot-starter-web:1.2.4.RELEASE") {
            exclude module: "spring-boot-starter-tomcat"
        }
        compile("org.springframework.boot:spring-boot-starter-jetty:1.2.4.RELEASE")
        // end::jetty[]
        // tag::actuator[]
        compile("org.springframework.boot:spring-boot-starter-actuator:1.2.4.RELEASE")
        // end::actuator[]
        // tag::tests[]
        testCompile("org.springframework.boot:spring-boot-starter-test:1.2.4.RELEASE")
        // end::tests[]

        compile project(":domain-model")
        compile project(":persistence")
    }
}
